# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Evangelos Foutras <evangelos@foutrelis.com>
#Credits: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lldb-git
pkgdesc='Next generation, high-performance debugger (git version)'
url='https://lldb.llvm.org/'
pkgver=15.0.0
commit=2db700215a2eebce7358c0a81a3d52d0a9d4a997
pkgrel=1
arch=(x86_64)
license=('custom:Apache 2.0 with LLVM Exception')
depends=(llvm-libs clang python python-six)
makedepends=(llvm cmake ninja swig python-sphinx git)
conflicts=(lldb)
provides=(lldb)
source=(git+https://github.com/llvm/llvm-project.git#commit=$commit)
md5sums=(SKIP)

pkgver() {
  cd ${srcdir}/llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "$_pkgver"
}

build(){
  cd ${srcdir}/llvm-project/lldb

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_SKIP_RPATH=ON \
  -DLLVM_LINK_LLVM_DYLIB=ON \
  -DCLANG_LINK_CLANG_DYLIB=ON \
  -DLLVM_ENABLE_SPHINX=ON \
  -DLLDB_USE_SYSTEM_SIX=1

  ninja -C build all
  ninja -C build docs-lldb-man
}

package(){
  DESTDIR="$pkgdir" ninja -C ${srcdir}/llvm-project/lldb/build/ install

  install -Dm644 ${srcdir}/llvm-project/lldb/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ${srcdir}/llvm-project/lldb/CODE_OWNERS.txt "$pkgdir/usr/share/licenses/$pkgname/CODE_OWNERS"

  install -Dm644 ${srcdir}/llvm-project/lldb/build/docs/man/lldb.1 "$pkgdir/usr/share/man/man1/lldb.1"
  install -Dm644 ${srcdir}/llvm-project/lldb/build/docs/man/lldb-server.1 "$pkgdir/usr/share/man/man1/lldb-server.1"

  # Compile Python scripts
  python3 -m compileall "$pkgdir"
  python3 -O -m compileall "$pkgdir"
  python3 -OO -m compileall "$pkgdir"
}
