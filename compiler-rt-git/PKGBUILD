# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Evangelos Foutras <evangelos@foutrelis.com>
#Credits: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=compiler-rt-git
pkgdesc='Compiler runtime libraries for clang (git version)'
url='https://compiler-rt.llvm.org/'
pkgver=15.0.0
commit=ed4e6e03981a37d54c4ac635b2bdb2c2fe8fec92
_pkgver=15.0.0
pkgrel=1
arch=(x86_64)
license=('custom:Apache 2.0 with LLVM Exception')
depends=(gcc-libs)
makedepends=(llvm cmake ninja python git)
# Build 32-bit compiler-rt libraries on x86_64 (FS#41911)
makedepends_x86_64=(lib32-gcc-libs)
options=(staticlibs)
conflicts=(compiler-rt)
provides=(compiler-rt)
source=(git+https://github.com/llvm/llvm-project.git#commit=$commit)
md5sums=(SKIP)

pkgver() {
  cd ${srcdir}/llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "$_pkgver"
}

build(){
  cd ${srcdir}/llvm-project/compiler-rt

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_SKIP_RPATH=ON \
  -DCOMPILER_RT_INSTALL_PATH=/usr/lib/clang/$_pkgver

  ninja -C build
}

package(){
  DESTDIR="$pkgdir" ninja -C ${srcdir}/llvm-project/compiler-rt/build/ install

  install -Dm644 ${srcdir}/llvm-project/compiler-rt/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ${srcdir}/llvm-project/compiler-rt/CREDITS.TXT "$pkgdir/usr/share/licenses/$pkgname/CREDITS"
}
