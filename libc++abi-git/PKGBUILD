#Maintainer: kevall474 <kevall474@tuta.io> <https://github.com/kevall474>
#Credits: Anatol Pomozov
#Credits: Llewelyn Trahaearn <woefulderelict [at] gmail [dot] com>
#Credits: Daniel Micay <danielmicay [at] gmail [dot] com>
#Credits: MThinkCpp <mtc [dot] maintainer [at] outlook [dot] com>

pkgname=libc++abi-git
url='https://libcxx.llvm.org/'
pkgver=14.0.0
pkgrel=1
license=('custom:Apache 2.0 with LLVM Exception')
arch=('x86_64')
depends=('gcc-libs')
makedepends=('clang' 'cmake' 'llvm' 'libunwind' 'ninja' 'python')
source=("llvm-project::git+https://github.com/llvm/llvm-project.git#branch=main")
md5sums=('SKIP')

pkgver() {
  cd llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "$_pkgver"
}

build(){
  cd llvm-project

  rm -rf build-libc++abi

  cmake -H. -G Ninja -Bbuild-libc++abi \
  -S libcxxabi  \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DLIBCXXABI_LIBCXX_INCLUDES=/usr/include/c++/v1/

  ninja -C build-libc++abi
}

package_libc++abi-git(){
  pkgdesc='Low level support for the LLVM C++ standard library (git version)'
  options=('staticlibs')
  conflicts=('libc++abi')
  provides=('libc++abi')

  DESTDIR="$pkgdir" ninja -C llvm-project/build-libc++abi/ install

  install -Dm0644 llvm-project/libcxxabi/CREDITS.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/CREDITS
  install -Dm0644 llvm-project/libcxxabi/LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
