# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=openmp-git
pkgdesc='LLVM OpenMP Runtime Library (git version)'
url='https://openmp.llvm.org/'
pkgver=15.0.0.r427244.gb911cbdcb9c4
commit=4022551a154e01ca5347629fd429ca04b36801e0
pkgrel=1
arch=(x86_64)
license=('custom:Apache 2.0 with LLVM Exception')
depends=(glibc libelf libffi)
makedepends=(cmake ninja git)
conflicts=(openmp)
provides=(openmp)
source=(git+https://github.com/llvm/llvm-project.git#commit=$commit)
md5sums=(SKIP)

pkgver() {
  cd ${srcdir}/llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "$_pkgver"
}

build(){
  cd ${srcdir}/llvm-project/openmp

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DLIBOMP_INSTALL_ALIASES=OFF \
  -DENABLE_LIBOMPTARGET=OFF

  ninja -C build
}

package(){
  DESTDIR="$pkgdir" ninja -C ${srcdir}/llvm-project/openmp/build/ install

  install -Dm644 "$srcdir/llvm-project/openmp/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/llvm-project/openmp/CREDITS.txt" "$pkgdir/usr/share/licenses/$pkgname/CREDITS"

  rm "$pkgdir/usr/lib/libarcher_static.a"
}
