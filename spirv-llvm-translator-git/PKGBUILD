#Maintainer: kevall474 <kevall474@tuta.io> <https://github.com/kevall474>
#Credits: Daniel Bermond <dbermond@archlinux.org>
#Credits: Bruno Pagani <archange@archlinux.org>

#######################################

#Set '1' to build with GCC
#Set '2' to build with CLANG
#Default is empty. It will build with GCC. To build with different compiler just use : env _compiler=(1 or 2) makepkg -s
if [ -z ${_compiler+x} ]; then
  _compiler=
fi

#######################################

pkgname=spirv-llvm-translator-git
pkgdesc='Tool and a library for bi-directional translation between SPIR-V and LLVM IR (git version)'
url='https://github.com/KhronosGroup/SPIRV-LLVM-Translator/'
pkgver=14.0.0.r1400.g4d66fe7f
pkgrel=1
arch=('x86_64')
license=(custom)
depends=('llvm-libs')
makedepends=('cmake' 'llvm' 'spirv-tools' 'spirv-headers' 'python' 'python-setuptools' 'clang' 'make' 'ninja' 'gcc' 'gcc-libs')
conflicts=('spirv-llvm-translator')
provides=('spirv-llvm-translator')
source=('SPIRV-LLVM-Translator::git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator.git')
md5sums=('SKIP')

pkgver(){
  cd SPIRV-LLVM-Translator

  echo 14.0.0.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
}

prepare(){
  sed -i 's/addRetAttr/addFnAttr/' $srcdir/SPIRV-LLVM-Translator/lib/SPIRV/SPIRVReader.cpp
}

build() {
if [[ $_compiler = "1" ]]; then
  export CC="gcc -m64"
  export CXX="g++ -m64"
elif [[ $_compiler = "2" ]]; then
  export CC="clang -m64"
  export CXX="clang++ -m64"
else
  export CC="gcc -m64"
  export CXX="g++ -m64"
fi

  cd SPIRV-LLVM-Translator

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DBUILD_SHARED_LIBS=ON \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
  -DCMAKE_SKIP_RPATH=ON \
  -DLLVM_INCLUDE_TESTS=ON \
  -DLLVM_EXTERNAL_LIT=/usr/bin/lit \
  -DLLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr

  ninja -C build
  ninja -C build llvm-spirv
}

package(){
    DESTDIR="${pkgdir}" ninja -C SPIRV-LLVM-Translator/build/ install

    install -Dm755 SPIRV-LLVM-Translator/build/tools/llvm-spirv/llvm-spirv -t "${pkgdir}"/usr/bin

    install -Dm644 SPIRV-LLVM-Translator/LICENSE.TXT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
