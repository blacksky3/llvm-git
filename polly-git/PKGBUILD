#Maintainer: kevall474 <kevall474@tuta.io> <https://github.com/kevall474>
#Credits: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=polly-git
pkgdesc='High-level loop and data-locality optimizer and optimization infrastructure for LLVM (git version)'
url='https://polly.llvm.org/'
pkgver=14.0.0
pkgrel=1
arch=('x86_64')
license=('custom:Apache 2.0 with LLVM Exception')
depends=('gcc-libs')
makedepends=('llvm' 'cmake' 'ninja' 'python-sphinx')
conflicts=('polly')
provides=('polly')
source=("llvm-project::git+https://github.com/llvm/llvm-project.git#branch=main")
md5sums=('SKIP')

pkgver() {
  cd llvm-project/llvm

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  CMakeLists.txt).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "$_pkgver"
}

build(){
  cd llvm-project/polly

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DLLVM_LINK_LLVM_DYLIB=ON \
  -DLLVM_BUILD_TESTS=ON \
  -DLLVM_BUILD_DOCS=OFF \
  -DLLVM_ENABLE_SPHINX=OFF \
  -DSPHINX_WARNINGS_AS_ERRORS=OFF \
  -DLLVM_EXTERNAL_LIT=/usr/bin/lit \
  -DLLVM_BUILD_MAIN_SRC_DIR="$srcdir/llvm-project/llvm"

  ninja -C build/
}

package(){
  cd llvm-project/polly

  DESTDIR="$pkgdir" ninja -C build/ install

  install -Dm644 "$srcdir/llvm-project/polly/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/llvm-project/polly/CREDITS.txt" "$pkgdir/usr/share/licenses/$pkgname/CREDITS"

  # Remove documentation sources
  #rm -r "$pkgdir"/usr/share/doc/polly/html/{_sources,.buildinfo}
}
